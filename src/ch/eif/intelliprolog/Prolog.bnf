{
  parserClass="ch.eif.intelliprolog.PrologParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Prolog"
  psiImplClassSuffix="Impl"
  psiPackage="ch.eif.intelliprolog.psi"
  psiImplPackage="ch.eif.intelliprolog.psi.impl"

  elementTypeHolderClass="ch.eif.intelliprolog.psi.PrologTypes"
  elementTypeClass="ch.eif.intelliprolog.psi.PrologElementType"
  tokenTypeClass="ch.eif.intelliprolog.psi.PrologTokenType"
}

prologFile ::= item_*

private item_ ::= (sentence|COMMENT|CRLF)

sentence ::= (operation|compound|atom) DOT

term ::= (operation|basic_term) //basic_term instead of term to avoid left recursion

basic_term ::= (parenthesized_block|braced_block|list|number|variable|STRING|compound|atom)

operator ::= (UNQUOTED_ATOM|SYMBOLIC_ATOM)

operation ::= (native_binary_operation|native_left_operation)

//operation ::= basic_term{2}

/*operation ::= (native_binary_operation|native_left_operation|
                custom_binary_operation|custom_left_operation)*/

native_binary_operation ::= basic_term known_binary_operator term

native_left_operation ::= known_left_operator term


atom ::= (
            UNQUOTED_ATOM|
            SYMBOLIC_ATOM|
            QUOTED_ATOM
         )


compound ::= compound_name LPAREN term RPAREN

compound_name ::= (
                    UNQUOTED_COMPOUND_NAME|
                    QUOTED_COMPOUND_NAME
                  )

list ::= LBRACKET term? RBRACKET

braced_block ::= LBRACE term RBRACE

parenthesized_block ::= LPAREN term RPAREN

number ::= (INTEGER|FLOAT|BIN_NUMBER|OCT_NUMBER|HEX_NUMBER|CHAR_CODE)

variable ::= (ANONYMOUS_VARIABLE|NAMED_VARIABLE)
//known_operator ::= (known_binary_operator|known_left_operator)

valid_operator ::= UNQUOTED_ATOM|SYMBOLIC_ATOM


known_binary_operator ::= &valid_operator (
                            '-->'|
                            ':-'|
                            ';'|'|'|
                            '->'|'*->'|
                            ','|
                            '<'|'='|'=..'|
                            '=:='|'=<'|'=='|'=\='|
                            '>'|'>='|'@<'|'@=<'|
                            '@>'|'@>='|'\='|'\=='|
                            'as'|'is'|
                            ':'|
                            '+'|'-'|'/\'|'\/'|'xor'|
                            '*'|'/'|'//'|'div'|'>>'|'mod'|'rem'|
                            '#<'|'#>'|'#='|'#\='|'#<='|'#>='|'#=#'|
                            '#<=>'|'#\<=>'|'#==>'|'#\==>'|'#/\'|'#\/\'|'#\/'|'#\\/'|'##'|'#\'|
                            '**'|
                            '^'|
                            '::'|
                            '^^'|
                            '<<'
                            )



known_left_operator ::= &valid_operator (
                            ':-'|
                            '\+'|
                            '?'|
                            '+'|'-'|'\'|
                            '++'|'--'|
                            '$'|
                            '@'|
                            '::'|
                            '^^'
                            )